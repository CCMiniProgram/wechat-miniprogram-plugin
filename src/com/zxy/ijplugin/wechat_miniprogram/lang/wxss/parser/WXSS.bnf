/*
 *    Copyright (c) [2019] [zxy]
 *    [wechat-miniprogram-plugin] is licensed under the Mulan PSL v1.
 *    You can use this software according to the terms and conditions of the Mulan PSL v1.
 *    You may obtain a copy of Mulan PSL v1 at:
 *       http://license.coscl.org.cn/MulanPSL
 *    THIS SOFTWARE IS PROVIDED ON AN "AS IS" BASIS, WITHOUT WARRANTIES OF ANY KIND, EITHER EXPRESS OR
 *    IMPLIED, INCLUDING BUT NOT LIMITED TO NON-INFRINGEMENT, MERCHANTABILITY OR FIT FOR A PARTICULAR
 *    PURPOSE.
 *    See the Mulan PSL v1 for more details.
 *
 *
 *                      Mulan Permissive Software License，Version 1
 *
 *    Mulan Permissive Software License，Version 1 (Mulan PSL v1)
 *    August 2019 http://license.coscl.org.cn/MulanPSL
 *
 *    Your reproduction, use, modification and distribution of the Software shall be subject to Mulan PSL v1 (this License) with following terms and conditions:
 *
 *    0. Definition
 *
 *       Software means the program and related documents which are comprised of those Contribution and licensed under this License.
 *
 *       Contributor means the Individual or Legal Entity who licenses its copyrightable work under this License.
 *
 *       Legal Entity means the entity making a Contribution and all its Affiliates.
 *
 *       Affiliates means entities that control, or are controlled by, or are under common control with a party to this License, ‘control’ means direct or indirect ownership of at least fifty percent (50%) of the voting power, capital or other securities of controlled or commonly controlled entity.
 *
 *    Contribution means the copyrightable work licensed by a particular Contributor under this License.
 *
 *    1. Grant of Copyright License
 *
 *       Subject to the terms and conditions of this License, each Contributor hereby grants to you a perpetual, worldwide, royalty-free, non-exclusive, irrevocable copyright license to reproduce, use, modify, or distribute its Contribution, with modification or not.
 *
 *    2. Grant of Patent License
 *
 *       Subject to the terms and conditions of this License, each Contributor hereby grants to you a perpetual, worldwide, royalty-free, non-exclusive, irrevocable (except for revocation under this Section) patent license to make, have made, use, offer for sale, sell, import or otherwise transfer its Contribution where such patent license is only limited to the patent claims owned or controlled by such Contributor now or in future which will be necessarily infringed by its Contribution alone, or by combination of the Contribution with the Software to which the Contribution was contributed, excluding of any patent claims solely be infringed by your or others’ modification or other combinations. If you or your Affiliates directly or indirectly (including through an agent, patent licensee or assignee）, institute patent litigation (including a cross claim or counterclaim in a litigation) or other patent enforcement activities against any individual or entity by alleging that the Software or any Contribution in it infringes patents, then any patent license granted to you under this License for the Software shall terminate as of the date such litigation or activity is filed or taken.
 *
 *    3. No Trademark License
 *
 *       No trademark license is granted to use the trade names, trademarks, service marks, or product names of Contributor, except as required to fulfill notice requirements in section 4.
 *
 *    4. Distribution Restriction
 *
 *       You may distribute the Software in any medium with or without modification, whether in source or executable forms, provided that you provide recipients with a copy of this License and retain copyright, patent, trademark and disclaimer statements in the Software.
 *
 *    5. Disclaimer of Warranty and Limitation of Liability
 *
 *       The Software and Contribution in it are provided without warranties of any kind, either express or implied. In no event shall any Contributor or copyright holder be liable to you for any damages, including, but not limited to any direct, or indirect, special or consequential damages arising from your use or inability to use the Software or the Contribution in it, no matter how it’s caused or based on which legal theory, even if advised of the possibility of such damages.
 *
 *    End of the Terms and Conditions
 *
 *    How to apply the Mulan Permissive Software License，Version 1 (Mulan PSL v1) to your software
 *
 *       To apply the Mulan PSL v1 to your work, for easy identification by recipients, you are suggested to complete following three steps:
 *
 *       i. Fill in the blanks in following statement, including insert your software name, the year of the first publication of your software, and your name identified as the copyright owner;
 *       ii. Create a file named “LICENSE” which contains the whole context of this License in the first directory of your software package;
 *       iii. Attach the statement to the appropriate annotated syntax at the beginning of each source file.
 *
 *    Copyright (c) [2019] [name of copyright holder]
 *    [Software Name] is licensed under the Mulan PSL v1.
 *    You can use this software according to the terms and conditions of the Mulan PSL v1.
 *    You may obtain a copy of Mulan PSL v1 at:
 *       http://license.coscl.org.cn/MulanPSL
 *    THIS SOFTWARE IS PROVIDED ON AN "AS IS" BASIS, WITHOUT WARRANTIES OF ANY KIND, EITHER EXPRESS OR
 *    IMPLIED, INCLUDING BUT NOT LIMITED TO NON-INFRINGEMENT, MERCHANTABILITY OR FIT FOR A PARTICULAR
 *    PURPOSE.
 *
 *    See the Mulan PSL v1 for more details.
 */

{
  parserClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.parser.WXSSParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="WXSS"
  psiImplClassSuffix="Impl"
  psiPackage="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi"
  psiImplPackage="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.impl"

  elementTypeHolderClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSTypes"
  elementTypeClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSElementType"
  tokenTypeClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSTokenType"

  psiImplUtilClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.impl.WXSSPsiImplUtils"

  tokens = [
    COMMENT = 'regexp:/*..*/'
    LEFT_BRACKET = '{'
    RIGHT_BRACKET = '}'
    LEFT_PARENTHESES = '('
    RIGHT_PARENTHESES = ')'
    STRING_START_DQ = '"'
    STRING_START_SQ = "'"
    STRING_END_DQ = '"'
    STRING_END_SQ = "'"
  ]
}

wxssFile ::= (import | styleDefinition | CRLF | fontDefinition)*
import ::= (IMPORT_KEYWORD string SEMICOLON) {
    pin = 1
}
string ::= ( doubleQuoteString | singleQuoteString)
private doubleQuoteString ::= STRING_START_DQ stringText? STRING_END_DQ {
    pin = 1
}
private singleQuoteString ::= STRING_START_SQ stringText? STRING_END_SQ {
    pin = 1
}
stringText ::= STRING_CONTENT {
    implements = ["com.intellij.psi.ContributedReferenceHost" "com.intellij.psi.PsiLanguageInjectionHost" ]
    methods =[createLiteralTextEscaper isValidHost updateText getReferences]
}
styleDefinition ::= selectorGroup  styleStatementSection {
    pin = 1
}
selectorGroup ::= (selectors (COMMA selectors)*) {
    pin = selectors
}
selectors ::= (selector)+
selector ::= (idSelector | classSelector | ELEMENT_NAME)(PSEUDO_SELECTOR)?
idSelector ::= NUMBER_SIGN ID {
    implements="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElement"
    methods=[getPresentation getId getNameIdentifier setName getName getIcon getReference getTextOffset getReferences getUseScope]
    mixin="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElementImpl"
    pin = 1
}
classSelector ::= DOT CLASS {
    implements=["com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElement"]
    methods=[getPresentation getClassName getNameIdentifier setName getName getIcon getReference getTextOffset getReferences getUseScope]
    mixin="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElementImpl"
    pin = 1
}
styleStatementSection ::= LEFT_BRACKET styleStatementCollection? RIGHT_BRACKET {
    pin = 1
}
styleStatementCollection ::= styleStatement (SEMICOLON styleStatement)* SEMICOLON? {
    pin = styleStatement
}
styleStatement ::= ATTRIBUTE_NAME COLON attributeValue IMPORTANT_KEYWORD?{
    pin = 1
}
attributeValue ::= valueGroup (COMMA valueGroup)*
private valueGroup ::= (value)+
value ::= (HASH | function | numericalValue | string | ATTRIBUTE_VALUE_LITERAL | UNICODE_RANGE)
function ::= FUNCTION_NAME LEFT_PARENTHESES functionArgs? RIGHT_PARENTHESES {
    pin = 2
}
functionArgs ::= (functionArgGroup (COMMA functionArgGroup)*)
private functionArgGroup ::= (functionArg)+
functionArg ::= (HASH | numericalValue | string )
numericalValue ::= NUMBER NUMBER_UNIT?


fontDefinition ::= FONT_FACE_KEYWORD styleStatementSection {
    pin = 1
}