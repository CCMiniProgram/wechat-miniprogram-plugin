{
  parserClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.parser.WXSSParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="WXSS"
  psiImplClassSuffix="Impl"
  psiPackage="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi"
  psiImplPackage="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.impl"

  elementTypeHolderClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSTypes"
  elementTypeClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSElementType"
  tokenTypeClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSTokenType"

  psiImplUtilClass="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.impl.WXSSPsiImplUtils"

  tokens = [
    COMMENT = 'regexp:/*..*/'
    LEFT_BRACKET = '{'
    RIGHT_BRACKET = '}'
    LEFT_PARENTHESES = '('
    RIGHT_PARENTHESES = ')'
    STRING_START_DQ = '"'
    STRING_START_SQ = "'"
    STRING_END_DQ = '"'
    STRING_END_SQ = "'"
  ]
}

wxssFile ::= (import | styleDefinition | CRLF | fontDefinition)*
import ::= (IMPORT_KEYWORD string SEMICOLON) {
    pin = 1
}
string ::= ( doubleQuoteString | singleQuoteString)
private doubleQuoteString ::= STRING_START_DQ stringText? STRING_END_DQ {
    pin = 1
}
private singleQuoteString ::= STRING_START_SQ stringText? STRING_END_SQ {
    pin = 1
}
stringText ::= STRING_CONTENT {
    implements = ["com.intellij.psi.ContributedReferenceHost" "com.intellij.psi.PsiLanguageInjectionHost" ]
    methods =[createLiteralTextEscaper isValidHost updateText getReferences]
}
styleDefinition ::= selectorGroup  styleStatementSection {
    pin = 1
}
selectorGroup ::= (selectors (COMMA selectors)*) {
    pin = selectors
}
selectors ::= (selector)+
selector ::= (idSelector | classSelector | ELEMENT_NAME)(PSEUDO_SELECTOR)?
idSelector ::= NUMBER_SIGN ID {
    implements="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElement"
    methods=[getPresentation getId getNameIdentifier setName getName getIcon getReference getTextOffset]
    mixin="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElementImpl"
    pin = 1
}
classSelector ::= DOT CLASS {
    implements=["com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElement"]
    methods=[getPresentation getClassName getNameIdentifier setName getName getIcon getReference getTextOffset getReferences]
    mixin="com.zxy.ijplugin.wechat_miniprogram.lang.wxss.psi.WXSSNamedElementImpl"
    pin = 1
}
styleStatementSection ::= LEFT_BRACKET styleStatementCollection RIGHT_BRACKET {
    pin = 1
}
styleStatementCollection ::= (styleStatement SEMICOLON)* styleStatement? {
    pin = styleStatement
}
styleStatement ::= ATTRIBUTE_NAME COLON attributeValue {
    pin = 1
}
attributeValue ::= valueGroup (COMMA valueGroup)*
private valueGroup ::= (value)+
value ::= (HASH | function | numericalValue | string | ATTRIBUTE_VALUE_LITERAL | UNICODE_RANGE)
function ::= FUNCTION_NAME LEFT_PARENTHESES functionArgs? RIGHT_PARENTHESES {
    pin = 2
}
functionArgs ::= (functionArgGroup (COMMA functionArgGroup)*)
private functionArgGroup ::= (functionArg)+
functionArg ::= (HASH | numericalValue | string )
numericalValue ::= NUMBER NUMBER_UNIT?


fontDefinition ::= FONT_FACE_KEYWORD styleStatementSection {
    pin = 1
}